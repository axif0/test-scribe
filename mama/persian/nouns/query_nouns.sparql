# tool: scribe-data
# All Persian (Q9168) nouns (Q1084) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nouns
  ?pluralAnimate
  ?singular
  ?dual
  ?positive

WHERE {
  ?lexeme dct:language wd:Q9168 ;
  wikibase:lexicalCategory wd:Q1084 ;
  wikibase:lemma ?nouns .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralAnimateForm .
    ?pluralAnimateForm ontolex:representation ?pluralAnimate ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q51927507 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dualForm .
    ?dualForm ontolex:representation ?dual ;
      wikibase:grammaticalFeature wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?positiveForm .
    ?positiveForm ontolex:representation ?positive ;
      wikibase:grammaticalFeature wd:Q3482678 .
  }
}
