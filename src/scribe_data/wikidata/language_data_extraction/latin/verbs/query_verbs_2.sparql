# tool: scribe-data
# All Latin (Q397) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?activePresentInfinitive
  ?passivePerfectParticiple
  ?passivePresentIndicativeFirstPersonPlural
  ?indicativeActivePresentSecondPersonPlural
  ?activeImperativePresentSecondPersonPlural
  ?activePerfectFirstPersonPluralSubjunctive

WHERE {
  ?lexeme dct:language wd:Q397 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePresentInfinitiveForm .
    ?activePresentInfinitiveForm ontolex:representation ?activePresentInfinitive ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q52434245 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePerfectParticipleForm .
    ?passivePerfectParticipleForm ontolex:representation ?passivePerfectParticiple ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q625420, wd:Q814722 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePresentIndicativeFirstPersonPluralForm .
    ?passivePresentIndicativeFirstPersonPluralForm ontolex:representation ?passivePresentIndicativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q56682909, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeActivePresentSecondPersonPluralForm .
    ?indicativeActivePresentSecondPersonPluralForm ontolex:representation ?indicativeActivePresentSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q1317831, wd:Q192613, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeImperativePresentSecondPersonPluralForm .
    ?activeImperativePresentSecondPersonPluralForm ontolex:representation ?activeImperativePresentSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q22716, wd:Q192613, wd:Q51929049, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePerfectFirstPersonPluralSubjunctiveForm .
    ?activePerfectFirstPersonPluralSubjunctiveForm ontolex:representation ?activePerfectFirstPersonPluralSubjunctive ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q625420, wd:Q21714344, wd:Q146786, wd:Q473746 .
  }
}
