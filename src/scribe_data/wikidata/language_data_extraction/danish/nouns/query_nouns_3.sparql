# tool: scribe-data
# All Danish (Q9035) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?plural
  ?genitiveIndefinite
  ?nonGenitiveIndefinite
  ?nonGenitiveIndefiniteDefinite
  ?commonDefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q9035 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveIndefiniteForm .
    ?genitiveIndefiniteForm ontolex:representation ?genitiveIndefinite ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveIndefiniteForm .
    ?nonGenitiveIndefiniteForm ontolex:representation ?nonGenitiveIndefinite ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nonGenitiveIndefiniteDefiniteForm .
    ?nonGenitiveIndefiniteDefiniteForm ontolex:representation ?nonGenitiveIndefiniteDefinite ;
      wikibase:grammaticalFeature wd:Q98946930, wd:Q53997857, wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?commonDefiniteSingularForm .
    ?commonDefiniteSingularForm ontolex:representation ?commonDefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1305037, wd:Q53997851, wd:Q110786 .
  }
}
