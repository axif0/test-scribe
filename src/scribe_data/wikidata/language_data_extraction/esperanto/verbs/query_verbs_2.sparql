# tool: scribe-data
# All Esperanto (Q143) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?future
  ?presentIndicative
  ?subjunctive
  ?futurePresentParticiple

WHERE {
  ?lexeme dct:language wd:Q143 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
      wikibase:grammaticalFeature wd:Q501405 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentIndicativeForm .
    ?presentIndicativeForm ontolex:representation ?presentIndicative ;
      wikibase:grammaticalFeature wd:Q56682909 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?subjunctiveForm .
    ?subjunctiveForm ontolex:representation ?subjunctive ;
      wikibase:grammaticalFeature wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futurePresentParticipleForm .
    ?futurePresentParticipleForm ontolex:representation ?futurePresentParticiple ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q10345583 .
  }
}
