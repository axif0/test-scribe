# tool: scribe-data
# All Bengali (Q9610) verbs (Q24905) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verbs
  ?thirdPersonSimplePresent
  ?firstPersonSimplePast
  ?simplePresentFirstPerson
  ?thirdPersonSimplePast
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q9610 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verbs ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSimplePresentForm .
    ?thirdPersonSimplePresentForm ontolex:representation ?thirdPersonSimplePresent ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q3910936 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSimplePastForm .
    ?firstPersonSimplePastForm ontolex:representation ?firstPersonSimplePast ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q1392475 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentFirstPersonForm .
    ?simplePresentFirstPersonForm ontolex:representation ?simplePresentFirstPerson ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q21714344 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSimplePastForm .
    ?thirdPersonSimplePastForm ontolex:representation ?thirdPersonSimplePast ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q1392475 .
  }
}
