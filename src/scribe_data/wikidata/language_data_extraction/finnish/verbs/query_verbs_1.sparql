# tool: scribe-data
# All Finnish (Q1412) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?illativeInfinitive
  ?indicativePastImperfectThirdPerson
  ?imperativeFirstPersonPlural
  ?indicativePresentThirdPersonSingular
  ?indicativePastFirstPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1412 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativeInfinitiveForm .
    ?illativeInfinitiveForm ontolex:representation ?illativeInfinitive ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativePastImperfectThirdPersonForm .
    ?indicativePastImperfectThirdPersonForm ontolex:representation ?indicativePastImperfectThirdPerson ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q12547192, wd:Q51929074 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeFirstPersonPluralForm .
    ?imperativeFirstPersonPluralForm ontolex:representation ?imperativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativePresentThirdPersonSingularForm .
    ?indicativePresentThirdPersonSingularForm ontolex:representation ?indicativePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q192613, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativePastFirstPersonSingularForm .
    ?indicativePastFirstPersonSingularForm ontolex:representation ?indicativePastFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q1994301, wd:Q21714344, wd:Q110786 .
  }
}
