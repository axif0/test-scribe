# tool: scribe-data
# All Finnish (Q1412) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?illativeInfinitive
  ?indicativeFirstPersonSingular
  ?indicativeFirstPersonPlural
  ?indicativeSecondPersonSingular
  ?indicativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q1412 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativeInfinitiveForm .
    ?illativeInfinitiveForm ontolex:representation ?illativeInfinitive ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeFirstPersonSingularForm .
    ?indicativeFirstPersonSingularForm ontolex:representation ?indicativeFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeFirstPersonPluralForm .
    ?indicativeFirstPersonPluralForm ontolex:representation ?indicativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeSecondPersonSingularForm .
    ?indicativeSecondPersonSingularForm ontolex:representation ?indicativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indicativeSecondPersonPluralForm .
    ?indicativeSecondPersonPluralForm ontolex:representation ?indicativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q682111, wd:Q51929049, wd:Q146786 .
  }
}
