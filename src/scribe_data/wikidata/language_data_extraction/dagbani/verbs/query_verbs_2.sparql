# tool: scribe-data
# All Dagbani (Q32238) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?presentSingular
  ?presentPlural
  ?presentContinuousPlural
  ?pastSingular
  ?firstPersonSingular
  ?firstPersonPlural
  ?thirdPersonPlural
  ?imperativeSecondPersonSingular
  ?imperativeSecondPersonPlural
   ?lastModified

WHERE {
  ?lexeme dct:language wd:Q32238 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentSingularForm .
    ?presentSingularForm ontolex:representation ?presentSingular ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentPluralForm .
    ?presentPluralForm ontolex:representation ?presentPlural ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousPluralForm .
    ?presentContinuousPluralForm ontolex:representation ?presentContinuousPlural ;
      wikibase:grammaticalFeature wd:Q7240943, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastSingularForm .
    ?pastSingularForm ontolex:representation ?pastSingular ;
      wikibase:grammaticalFeature wd:Q1994301, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonSingularForm .
    ?firstPersonSingularForm ontolex:representation ?firstPersonSingular ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?firstPersonPluralForm .
    ?firstPersonPluralForm ontolex:representation ?firstPersonPlural ;
      wikibase:grammaticalFeature wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonPluralForm .
    ?thirdPersonPluralForm ontolex:representation ?thirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonSingularForm .
    ?imperativeSecondPersonSingularForm ontolex:representation ?imperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonPluralForm .
    ?imperativeSecondPersonPluralForm ontolex:representation ?imperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q146786 .
  }
}
