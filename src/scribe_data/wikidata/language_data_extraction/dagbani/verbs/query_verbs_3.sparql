# tool: scribe-data
# All Dagbani (Q32238) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePresent
  ?pastImperfect
  ?definite
  ?adverbial
  ?imperativeSecondPersonSingular
  ?imperativeSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q32238 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentForm .
    ?simplePresentForm ontolex:representation ?simplePresent ;
      wikibase:grammaticalFeature wd:Q3910936 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastImperfectForm .
    ?pastImperfectForm ontolex:representation ?pastImperfect ;
      wikibase:grammaticalFeature wd:Q12547192 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteForm .
    ?definiteForm ontolex:representation ?definite ;
      wikibase:grammaticalFeature wd:Q53997851 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?adverbialForm .
    ?adverbialForm ontolex:representation ?adverbial ;
      wikibase:grammaticalFeature wd:Q380012 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonSingularForm .
    ?imperativeSecondPersonSingularForm ontolex:representation ?imperativeSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeSecondPersonPluralForm .
    ?imperativeSecondPersonPluralForm ontolex:representation ?imperativeSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929049, wd:Q146786 .
  }
}
