# tool: scribe-data
# All Estonian (Q9072) prepositions (Q4833830) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?preposition
  ?elativeSingular
  ?elativePlural
  ?terminativeSingular
  ?terminativePlural
  ?essiveSingular
  ?essivePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
      wikibase:lexicalCategory wd:Q4833830 ;
      wikibase:lemma ?preposition ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?elativeSingularForm .
    ?elativeSingularForm ontolex:representation ?elativeSingular ;
      wikibase:grammaticalFeature wd:Q394253, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?elativePluralForm .
    ?elativePluralForm ontolex:representation ?elativePlural ;
      wikibase:grammaticalFeature wd:Q394253, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?terminativeSingularForm .
    ?terminativeSingularForm ontolex:representation ?terminativeSingular ;
      wikibase:grammaticalFeature wd:Q747019, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?terminativePluralForm .
    ?terminativePluralForm ontolex:representation ?terminativePlural ;
      wikibase:grammaticalFeature wd:Q747019, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?essiveSingularForm .
    ?essiveSingularForm ontolex:representation ?essiveSingular ;
      wikibase:grammaticalFeature wd:Q148465, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?essivePluralForm .
    ?essivePluralForm ontolex:representation ?essivePlural ;
      wikibase:grammaticalFeature wd:Q148465, wd:Q146786 .
  }
}
