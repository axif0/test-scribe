# tool: scribe-data
# All Estonian (Q9072) postpositions (Q161873) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?postposition
  ?genitiveSingular
  ?genitivePlural
  ?translativeSingular
  ?translativePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
      wikibase:lexicalCategory wd:Q161873 ;
      wikibase:lemma ?postposition ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?translativeSingularForm .
    ?translativeSingularForm ontolex:representation ?translativeSingular ;
      wikibase:grammaticalFeature wd:Q950170, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?translativePluralForm .
    ?translativePluralForm ontolex:representation ?translativePlural ;
      wikibase:grammaticalFeature wd:Q950170, wd:Q146786 .
  }
}
