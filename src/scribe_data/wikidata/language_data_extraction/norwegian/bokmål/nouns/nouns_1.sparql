# tool: scribe-data
# All Norwegian (Q25167) nouns (Q1084) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?nouns
  ?indefinitePlural
  ?definitePlural
  ?definiteSingular
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q25167 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?nouns ;
      schema:dateModified ?lastModified .
    
  # Note: We need to filter for nb to remove bokm√•l (nb) words.
  FILTER(lang(?nouns) = "nb")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefinitePluralForm .
    ?indefinitePluralForm ontolex:representation ?indefinitePlural ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definitePluralForm .
    ?definitePluralForm ontolex:representation ?definitePlural ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }
}
