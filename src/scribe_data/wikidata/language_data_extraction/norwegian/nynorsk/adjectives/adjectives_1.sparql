# tool: scribe-data
# All Norwegian (Q25164) adjectives (Q34698) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjectives
  ?neuterIndefiniteSingular
  ?feminineMasculineIndefiniteSingular
  ?definiteSingular
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q25164 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjectives ;
      schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?adjectives) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularForm .
    ?neuterIndefiniteSingularForm ontolex:representation ?neuterIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularForm .
    ?feminineMasculineIndefiniteSingularForm ontolex:representation ?feminineMasculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }
}
