# tool: scribe-data
# All Norwegian (Q25164) proper_nouns (Q147276) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?proper_nouns
  ?indefinitePlural
  ?definitePlural
  ?definiteSingular
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q25164 ;
      wikibase:lexicalCategory wd:Q147276 ;
      wikibase:lemma ?proper_nouns ;
      schema:dateModified ?lastModified .
    
  # Note: We need to filter for nn to remove nynorsk (nn) words.
  FILTER(lang(?proper_nouns) = "nn")
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indefinitePluralForm .
    ?indefinitePluralForm ontolex:representation ?indefinitePlural ;
      wikibase:grammaticalFeature wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definitePluralForm .
    ?definitePluralForm ontolex:representation ?definitePlural ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }
}
