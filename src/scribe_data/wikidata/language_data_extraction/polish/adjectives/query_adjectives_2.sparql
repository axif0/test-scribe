# tool: scribe-data
# All Polish (Q809) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?accusative
  ?comparative
  ?superlative
  ?nominativePluralPositive
  ?accusativePluralPositive
  ?nominativeMasculineInanimateSingularPositive

WHERE {
  ?lexeme dct:language wd:Q809 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeForm .
    ?accusativeForm ontolex:representation ?accusative ;
      wikibase:grammaticalFeature wd:Q146078 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeForm .
    ?superlativeForm ontolex:representation ?superlative ;
      wikibase:grammaticalFeature wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralPositiveForm .
    ?nominativePluralPositiveForm ontolex:representation ?nominativePluralPositive ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativePluralPositiveForm .
    ?accusativePluralPositiveForm ontolex:representation ?accusativePluralPositive ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q146786, wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeMasculineInanimateSingularPositiveForm .
    ?nominativeMasculineInanimateSingularPositiveForm ontolex:representation ?nominativeMasculineInanimateSingularPositive ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q52943434, wd:Q110786, wd:Q3482678 .
  }
}
