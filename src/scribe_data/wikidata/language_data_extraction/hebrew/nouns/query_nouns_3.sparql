# tool: scribe-data
# All Hebrew (Q9288) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?singular
  ?masculineSingular
  ?masculinePlural
  ?singularConstruct
  ?masculineSingularConstruct
  ?masculinePluralConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
      wikibase:grammaticalFeature wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularConstructForm .
    ?singularConstructForm ontolex:representation ?singularConstruct ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularConstructForm .
    ?masculineSingularConstructForm ontolex:representation ?masculineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralConstructForm .
    ?masculinePluralConstructForm ontolex:representation ?masculinePluralConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q1641446 .
  }
}
