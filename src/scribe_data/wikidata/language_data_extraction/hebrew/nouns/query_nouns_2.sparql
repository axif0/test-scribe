# tool: scribe-data
# All Hebrew (Q9288) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?femininePlural
  ?feminineDual
  ?masculineDual
  ?femininePluralConstruct
  ?feminineDualConstruct
  ?masculineDualConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralForm .
    ?femininePluralForm ontolex:representation ?femininePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineDualForm .
    ?feminineDualForm ontolex:representation ?feminineDual ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineDualForm .
    ?masculineDualForm ontolex:representation ?masculineDual ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralConstructForm .
    ?femininePluralConstructForm ontolex:representation ?femininePluralConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineDualConstructForm .
    ?feminineDualConstructForm ontolex:representation ?feminineDualConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110022, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineDualConstructForm .
    ?masculineDualConstructForm ontolex:representation ?masculineDualConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110022, wd:Q1641446 .
  }
}
