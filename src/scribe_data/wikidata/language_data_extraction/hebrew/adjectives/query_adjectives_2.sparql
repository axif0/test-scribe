# tool: scribe-data
# All Hebrew (Q9288) adjectives (Q34698) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjectives
  ?femininePluralConstruct
  ?feminineSingularConstruct
  ?masculineSingularConstruct
  ?masculinePluralConstruct
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q9288 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjectives ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralConstructForm .
    ?femininePluralConstructForm ontolex:representation ?femininePluralConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularConstructForm .
    ?feminineSingularConstructForm ontolex:representation ?feminineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularConstructForm .
    ?masculineSingularConstructForm ontolex:representation ?masculineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralConstructForm .
    ?masculinePluralConstructForm ontolex:representation ?masculinePluralConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q1641446 .
  }
}
