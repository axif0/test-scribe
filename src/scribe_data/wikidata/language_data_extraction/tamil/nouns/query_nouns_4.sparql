# tool: scribe-data
# All Tamil (Q5885) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?instrumental
  ?vocative
  ?instrumentalSingular
  ?instrumentalPlural
  ?vocativeSingular
  ?vocativePlural

WHERE {
  ?lexeme dct:language wd:Q5885 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalForm .
    ?instrumentalForm ontolex:representation ?instrumental ;
      wikibase:grammaticalFeature wd:Q192997 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeForm .
    ?vocativeForm ontolex:representation ?vocative ;
      wikibase:grammaticalFeature wd:Q185077 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalSingularForm .
    ?instrumentalSingularForm ontolex:representation ?instrumentalSingular ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalPluralForm .
    ?instrumentalPluralForm ontolex:representation ?instrumentalPlural ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeSingularForm .
    ?vocativeSingularForm ontolex:representation ?vocativeSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativePluralForm .
    ?vocativePluralForm ontolex:representation ?vocativePlural ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q146786 .
  }
}
