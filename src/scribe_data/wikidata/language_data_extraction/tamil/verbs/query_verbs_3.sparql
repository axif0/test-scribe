# tool: scribe-data
# All Tamil (Q5885) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?infinitive
  ?presentContinuous
  ?masculinePresentContinuous
  ?neuterSimplePast
  ?masculinePresentContinuousThirdPerson
  ?neuterSimplePastThirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q5885 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?infinitiveForm .
    ?infinitiveForm ontolex:representation ?infinitive ;
      wikibase:grammaticalFeature wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousForm .
    ?presentContinuousForm ontolex:representation ?presentContinuous ;
      wikibase:grammaticalFeature wd:Q7240943 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePresentContinuousForm .
    ?masculinePresentContinuousForm ontolex:representation ?masculinePresentContinuous ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q7240943 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSimplePastForm .
    ?neuterSimplePastForm ontolex:representation ?neuterSimplePast ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q1392475 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePresentContinuousThirdPersonForm .
    ?masculinePresentContinuousThirdPersonForm ontolex:representation ?masculinePresentContinuousThirdPerson ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q7240943, wd:Q51929074 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterSimplePastThirdPersonSingularForm .
    ?neuterSimplePastThirdPersonSingularForm ontolex:representation ?neuterSimplePastThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q1392475, wd:Q51929074, wd:Q110786 .
  }
}
