# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperative
  ?simplePresent
  ?combinedPresentParticiple
  ?simplePresentPlural
  ?thirdPersonSingular
  ?simplePresentFirstPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentForm .
    ?simplePresentForm ontolex:representation ?simplePresent ;
      wikibase:grammaticalFeature wd:Q3910936 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentPluralForm .
    ?simplePresentPluralForm ontolex:representation ?simplePresentPlural ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingularForm .
    ?thirdPersonSingularForm ontolex:representation ?thirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentFirstPersonSingularForm .
    ?simplePresentFirstPersonSingularForm ontolex:representation ?simplePresentFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q21714344, wd:Q110786 .
  }
}
