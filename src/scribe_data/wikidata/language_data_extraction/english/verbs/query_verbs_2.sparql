# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePast
  ?future
  ?pastParticiple
  ?simplePastPastParticiple
  ?preteriteSubjunctive
  ?thirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastForm .
    ?simplePastForm ontolex:representation ?simplePast ;
      wikibase:grammaticalFeature wd:Q1392475 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
      wikibase:grammaticalFeature wd:Q501405 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastPastParticipleForm .
    ?simplePastPastParticipleForm ontolex:representation ?simplePastPastParticiple ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteSubjunctiveForm .
    ?preteriteSubjunctiveForm ontolex:representation ?preteriteSubjunctive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?thirdPersonSingularForm .
    ?thirdPersonSingularForm ontolex:representation ?thirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q51929074, wd:Q110786 .
  }
}
