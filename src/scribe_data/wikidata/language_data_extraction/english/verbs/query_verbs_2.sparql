# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePastSingular
  ?simplePastPlural
  ?simplePastCombinedPastParticiple
  ?simplePastEnglishPastParticiple
  ?preteriteSubjunctive
  ?simplePresentThirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastSingularForm .
    ?simplePastSingularForm ontolex:representation ?simplePastSingular ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastPluralForm .
    ?simplePastPluralForm ontolex:representation ?simplePastPlural ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastCombinedPastParticipleForm .
    ?simplePastCombinedPastParticipleForm ontolex:representation ?simplePastCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastEnglishPastParticipleForm .
    ?simplePastEnglishPastParticipleForm ontolex:representation ?simplePastEnglishPastParticiple ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q1230649 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteSubjunctiveForm .
    ?preteriteSubjunctiveForm ontolex:representation ?preteriteSubjunctive ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q473746 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentThirdPersonSingularForm .
    ?simplePresentThirdPersonSingularForm ontolex:representation ?simplePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929074, wd:Q110786 .
  }
}
