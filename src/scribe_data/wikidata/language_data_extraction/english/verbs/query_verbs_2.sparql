# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?simplePresentPlural
  ?simplePastPastParticipleEnglish
  ?simplePresentFirstPersonSingular
  ?simplePresentThirdPersonSingular
  ?simplePastFirstPersonSingular
  ?simplePastThirdPersonSingular

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentPluralForm .
    ?simplePresentPluralForm ontolex:representation ?simplePresentPlural ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastPastParticipleEnglishForm .
    ?simplePastPastParticipleEnglishForm ontolex:representation ?simplePastPastParticipleEnglish ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q1230649 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentFirstPersonSingularForm .
    ?simplePresentFirstPersonSingularForm ontolex:representation ?simplePresentFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentThirdPersonSingularForm .
    ?simplePresentThirdPersonSingularForm ontolex:representation ?simplePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastFirstPersonSingularForm .
    ?simplePastFirstPersonSingularForm ontolex:representation ?simplePastFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastThirdPersonSingularForm .
    ?simplePastThirdPersonSingularForm ontolex:representation ?simplePastThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q51929074, wd:Q110786 .
  }
}
