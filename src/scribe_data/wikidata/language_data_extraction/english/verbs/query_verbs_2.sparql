# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?pastParticiple
  ?simplePastPastParticiple
   ?lastModified

WHERE {
  ?lexeme dct:language wd:Q1860 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q1230649 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastPastParticipleForm .
    ?simplePastPastParticipleForm ontolex:representation ?simplePastPastParticiple ;
      wikibase:grammaticalFeature wd:Q1392475, wd:Q1230649 .
  }
}
