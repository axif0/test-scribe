# tool: scribe-data
# All Arabic (Q13955) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?masculineSingular
  ?masculinePlural
  ?masculineDual
  ?genitivePluralConstruct
  ?masculineIndefiniteSingular
  ?genitiveAccusativeMasculineDual

WHERE {
  ?lexeme dct:language wd:Q13955 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineDualForm .
    ?masculineDualForm ontolex:representation ?masculineDual ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralConstructForm .
    ?genitivePluralConstructForm ontolex:representation ?genitivePluralConstruct ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786, wd:Q1641446 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineIndefiniteSingularForm .
    ?masculineIndefiniteSingularForm ontolex:representation ?masculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveAccusativeMasculineDualForm .
    ?genitiveAccusativeMasculineDualForm ontolex:representation ?genitiveAccusativeMasculineDual ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146078, wd:Q499327, wd:Q110022 .
  }
}
