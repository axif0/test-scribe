# tool: scribe-data
# All Arabic (Q13955) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?pausalIndefinite
  ?accusativeIndefiniteSingular
  ?accusativeIndefinitePlural
  ?pausalIndefiniteSingular
  ?pausalIndefinitePlural

WHERE {
  ?lexeme dct:language wd:Q13955 ;
      wikibase:lexicalCategory wd:Q1084 ;
      wikibase:lemma ?noun ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pausalIndefiniteForm .
    ?pausalIndefiniteForm ontolex:representation ?pausalIndefinite ;
      wikibase:grammaticalFeature wd:Q117262361, wd:Q53997857 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeIndefiniteSingularForm .
    ?accusativeIndefiniteSingularForm ontolex:representation ?accusativeIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeIndefinitePluralForm .
    ?accusativeIndefinitePluralForm ontolex:representation ?accusativeIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pausalIndefiniteSingularForm .
    ?pausalIndefiniteSingularForm ontolex:representation ?pausalIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q117262361, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pausalIndefinitePluralForm .
    ?pausalIndefinitePluralForm ontolex:representation ?pausalIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q117262361, wd:Q53997857, wd:Q146786 .
  }
}
