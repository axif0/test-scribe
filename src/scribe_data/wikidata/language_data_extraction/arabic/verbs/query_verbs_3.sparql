# tool: scribe-data
# All Arabic (Q13955) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?passiveParticiple
  ?performativeFirstPersonSingular
  ?masculinePerformativeThirdPersonSingular
  ?passivePerformativeFirstPersonSingular
  ?activeImperativeSecondPersonDual
  ?activePerfectFirstPersonSingular

WHERE {
  ?lexeme dct:language wd:Q13955 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveParticipleForm .
    ?passiveParticipleForm ontolex:representation ?passiveParticiple ;
      wikibase:grammaticalFeature wd:Q72249544 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?performativeFirstPersonSingularForm .
    ?performativeFirstPersonSingularForm ontolex:representation ?performativeFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q124351233, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePerformativeThirdPersonSingularForm .
    ?masculinePerformativeThirdPersonSingularForm ontolex:representation ?masculinePerformativeThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q124351233, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePerformativeFirstPersonSingularForm .
    ?passivePerformativeFirstPersonSingularForm ontolex:representation ?passivePerformativeFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q124351233, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeImperativeSecondPersonDualForm .
    ?activeImperativeSecondPersonDualForm ontolex:representation ?activeImperativeSecondPersonDual ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q22716, wd:Q51929049, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePerfectFirstPersonSingularForm .
    ?activePerfectFirstPersonSingularForm ontolex:representation ?activePerfectFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q625420, wd:Q21714344, wd:Q110786 .
  }
}
