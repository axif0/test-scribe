# tool: scribe-data
# All Arabic (Q13955) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?genitiveFeminineIndefiniteSingular
  ?genitiveFeminineIndefinitePlural
  ?genitiveFeminineIndefiniteDual
  ?genitiveMasculineIndefiniteSingular
  ?genitiveMasculineIndefinitePlural
  ?genitiveMasculineIndefiniteDual

WHERE {
  ?lexeme dct:language wd:Q13955 ;
      wikibase:lexicalCategory wd:Q34698 ;
      wikibase:lemma ?adjective ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveFeminineIndefiniteSingularForm .
    ?genitiveFeminineIndefiniteSingularForm ontolex:representation ?genitiveFeminineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q1775415, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveFeminineIndefinitePluralForm .
    ?genitiveFeminineIndefinitePluralForm ontolex:representation ?genitiveFeminineIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q1775415, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveFeminineIndefiniteDualForm .
    ?genitiveFeminineIndefiniteDualForm ontolex:representation ?genitiveFeminineIndefiniteDual ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q1775415, wd:Q53997857, wd:Q110022 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculineIndefiniteSingularForm .
    ?genitiveMasculineIndefiniteSingularForm ontolex:representation ?genitiveMasculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q53997857, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculineIndefinitePluralForm .
    ?genitiveMasculineIndefinitePluralForm ontolex:representation ?genitiveMasculineIndefinitePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q53997857, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveMasculineIndefiniteDualForm .
    ?genitiveMasculineIndefiniteDualForm ontolex:representation ?genitiveMasculineIndefiniteDual ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q499327, wd:Q53997857, wd:Q110022 .
  }
}
