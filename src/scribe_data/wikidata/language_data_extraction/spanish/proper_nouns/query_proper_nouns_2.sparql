# tool: scribe-data
# All Spanish (Q1321) proper_nouns (Q147276) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?proper_nouns
  ?singularFeminine
  ?singularMasculine
  ?pluralMasculine
  ?pluralFeminine
   ?lastModified

WHERE {
  ?lexeme dct:language wd:Q1321 ;
      wikibase:lexicalCategory wd:Q147276 ;
      wikibase:lemma ?proper_nouns ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularFeminineForm .
    ?singularFeminineForm ontolex:representation ?singularFeminine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineForm .
    ?singularMasculineForm ontolex:representation ?singularMasculine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineForm .
    ?pluralMasculineForm ontolex:representation ?pluralMasculine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineForm .
    ?pluralFeminineForm ontolex:representation ?pluralFeminine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415 .
  }
}
