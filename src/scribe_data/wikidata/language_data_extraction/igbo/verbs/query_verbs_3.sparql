# tool: scribe-data
# All Igbo (Q33578) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperative
  ?present
  ?past
  ?imperativeCombinedPastParticiple
  ?presentCombinedPresentParticiple
  ?pastCombinedPastParticiple

WHERE {
  ?lexeme dct:language wd:Q33578 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentForm .
    ?presentForm ontolex:representation ?present ;
      wikibase:grammaticalFeature wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeCombinedPastParticipleForm .
    ?imperativeCombinedPastParticipleForm ontolex:representation ?imperativeCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentCombinedPresentParticipleForm .
    ?presentCombinedPresentParticipleForm ontolex:representation ?presentCombinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q192613, wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastCombinedPastParticipleForm .
    ?pastCombinedPastParticipleForm ontolex:representation ?pastCombinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q1994301, wd:Q12717679 .
  }
}
