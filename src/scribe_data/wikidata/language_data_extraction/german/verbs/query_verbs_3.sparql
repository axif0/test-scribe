# tool: scribe-data
# All German (Q188) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?presentInfinitive
  ?preterite
  ?gerund
  ?imperativePerfectSingular
  ?imperativePerfectPlural

WHERE {
  ?lexeme dct:language wd:Q188 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentInfinitiveForm .
    ?presentInfinitiveForm ontolex:representation ?presentInfinitive ;
      wikibase:grammaticalFeature wd:Q52434245 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteForm .
    ?preteriteForm ontolex:representation ?preterite ;
      wikibase:grammaticalFeature wd:Q442485 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?gerundForm .
    ?gerundForm ontolex:representation ?gerund ;
      wikibase:grammaticalFeature wd:Q1923028 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePerfectSingularForm .
    ?imperativePerfectSingularForm ontolex:representation ?imperativePerfectSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q625420, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePerfectPluralForm .
    ?imperativePerfectPluralForm ontolex:representation ?imperativePerfectPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q625420, wd:Q146786 .
  }
}
