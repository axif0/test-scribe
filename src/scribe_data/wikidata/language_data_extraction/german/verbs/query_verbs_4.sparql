# tool: scribe-data
# All German (Q188) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?preteriteFirstPersonSingular
  ?preteriteFirstPersonPlural
  ?futureFirstPersonSingular
  ?futureFirstPersonPlural
  ?futureSecondPersonSingular
  ?futureSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q188 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteFirstPersonSingularForm .
    ?preteriteFirstPersonSingularForm ontolex:representation ?preteriteFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteFirstPersonPluralForm .
    ?preteriteFirstPersonPluralForm ontolex:representation ?preteriteFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureFirstPersonSingularForm .
    ?futureFirstPersonSingularForm ontolex:representation ?futureFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureFirstPersonPluralForm .
    ?futureFirstPersonPluralForm ontolex:representation ?futureFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureSecondPersonSingularForm .
    ?futureSecondPersonSingularForm ontolex:representation ?futureSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureSecondPersonPluralForm .
    ?futureSecondPersonPluralForm ontolex:representation ?futureSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q51929049, wd:Q146786 .
  }
}
