# tool: scribe-data
# All German (Q188) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?imperativePresentSingular
  ?imperativePresentPlural
  ?preteriteFirstPersonSingular
  ?preteriteFirstPersonPlural
  ?preteriteSecondPersonSingular
  ?preteriteSecondPersonPlural

WHERE {
  ?lexeme dct:language wd:Q188 ;
      wikibase:lexicalCategory wd:Q24905 ;
      wikibase:lemma ?verb ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePresentSingularForm .
    ?imperativePresentSingularForm ontolex:representation ?imperativePresentSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q192613, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativePresentPluralForm .
    ?imperativePresentPluralForm ontolex:representation ?imperativePresentPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q192613, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteFirstPersonSingularForm .
    ?preteriteFirstPersonSingularForm ontolex:representation ?preteriteFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteFirstPersonPluralForm .
    ?preteriteFirstPersonPluralForm ontolex:representation ?preteriteFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteSecondPersonSingularForm .
    ?preteriteSecondPersonSingularForm ontolex:representation ?preteriteSecondPersonSingular ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929049, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?preteriteSecondPersonPluralForm .
    ?preteriteSecondPersonPluralForm ontolex:representation ?preteriteSecondPersonPlural ;
      wikibase:grammaticalFeature wd:Q442485, wd:Q51929049, wd:Q146786 .
  }
}
