# tool: scribe-data
# All Italian (Q652) prepositions (Q4833830) and their forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?prepositions
  ?pluralMasculine
  ?singularMasculine
  ?pluralFeminine
  ?singularFeminine
  ?lastModified

WHERE {
  ?lexeme dct:language wd:Q652 ;
      wikibase:lexicalCategory wd:Q4833830 ;
      wikibase:lemma ?prepositions ;
      schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralMasculineForm .
    ?pluralMasculineForm ontolex:representation ?pluralMasculine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularMasculineForm .
    ?singularMasculineForm ontolex:representation ?singularMasculine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q499327 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralFeminineForm .
    ?pluralFeminineForm ontolex:representation ?pluralFeminine ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q1775415 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularFeminineForm .
    ?singularFeminineForm ontolex:representation ?singularFeminine ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q1775415 .
  }
}
